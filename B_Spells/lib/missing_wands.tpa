////////////////////////////////////////////////////////////////////////
//////////////////           Miscellaneous            //////////////////
////////////////////////////////////////////////////////////////////////

//ground bam
ACTION_IF NOT FILE_EXISTS_IN_GAME ~GWAND01.bam~ BEGIN
  COPY ~%folder%/data/arcane_crafting/wand/GWAND01.bam~ ~override~
END

//missing gems
ACTION_IF NOT FILE_EXISTS_IN_GAME ~MISC41.itm~ BEGIN
  COPY ~%folder%/data/arcane_crafting/wand/MISC41.itm~ ~override~
        SAY NAME1 @68087
        SAY NAME2 @68088
	SAY DESC @68089
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~TTTREAS.itm~ BEGIN
  COPY ~%folder%/data/arcane_crafting/wand/TTTREAS.itm~ ~override~
        SAY NAME1 @68087
        SAY NAME2 @68088
	SAY DESC @68089
END

//to do list
////////////////////////////////////////////////////////////////////////
//////////////////            Wand of Mirrors         //////////////////
////////////////////////////////////////////////////////////////////////
//| of Mirrors (N)  | ? |  Shield and Mirror Image |

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of Negation        //////////////////
////////////////////////////////////////////////////////////////////////
//| of Negation (N)  | ? |  Dispel Magic |

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of Peace           //////////////////
////////////////////////////////////////////////////////////////////////
//| of Peace (N) | ? |  All creatures in area save or cannot attack for 3 rounds |

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Revealing         //////////////////
////////////////////////////////////////////////////////////////////////
//| of Revealing (N)  | ? |  Find Traps/D. Illusion 75% |

////////////////////////////////////////////////////////////////////////
//////////////////             Wand of Salt           //////////////////
////////////////////////////////////////////////////////////////////////
//| of Salt (N) | ? | 6d8 dam plant cre (3d6 non plant) Save for 1/2 |


////////////////////////////////////////////////////////////////////////
//////////////////             Wand of Steam          //////////////////
////////////////////////////////////////////////////////////////////////
//| of Steam (N)  | ? |  As Cloudkill, but fire damage 3 r: 6d6/4d6/2d6 |

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of the Squid       //////////////////
////////////////////////////////////////////////////////////////////////
//| of the Squid (N)  | ? |  4d4 crush and hold (save 1/2 dam, no hold |

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of the Sun         //////////////////
////////////////////////////////////////////////////////////////////////
//| of the Sun (N)  | ? |  Ray 6d4 fire +2d4 metal wearing x2 undead|

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of Traps           //////////////////
////////////////////////////////////////////////////////////////////////
//| of Traps(N)  | ? |   as Find Traps |  Each charge just one 'check'

////////////////////////////////////////////////////////////////////////
//////////////////             Wand of Light          //////////////////
////////////////////////////////////////////////////////////////////////
//| of Light (N)  | VII |  Light Blade; Sun Ray (beam does 2d6+2 magic) x2 undead; Prot from evil; good only |

////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////          Tier I Wands              ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////
//////////////////      Wand of Magic Missiles        //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~WAND12.ITM~ BEGIN

  COPY_EXISTING ~WAND03.ITM~ ~override/WAND12.ITM~

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~LIGHT.ITM~ BEGIN

  COPY_EXISTING ~WAND03.ITM~ ~override/LIGHT.ITM~

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~PULS01.ITM~ BEGIN

  COPY_EXISTING ~WAND03.ITM~ ~override/PULS01.ITM~

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~PULS02.ITM~ BEGIN

  COPY_EXISTING ~WAND03.ITM~ ~override/PULS02.ITM~

END

////////////////////////////////////////////////////////////////////////
//////////////////             Mistletoe              //////////////////
////////////////////////////////////////////////////////////////////////
//| Mistletoe (N)  | I |  Shillelagh |

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN03.ITM~ BEGIN

  COPY ~%folder%/data/wands/B_WAN03.itm~ ~override~
        SAY NAME2 @900010005
        SAY DESC @900010006
END


////////////////////////////////////////////////////////////////////////
//////////////////             Wand of Fog            //////////////////
////////////////////////////////////////////////////////////////////////
//| of Fog (Ns) | I |  as Obscuring Mist |
//only installed if SR is installed 

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN11.ITM~ BEGIN

 ACTION_IF (FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_OBSCURING_MIST~)) BEGIN  //SR obscuring Mist
//copy the spell
 LAF RES_NUM_OF_SPELL_NAME
  STR_VAR spell_name = ~CLERIC_OBSCURING_MIST~
  RET spell_res
 END
 COPY_EXISTING ~%spell_res%.spl~ ~override/B_WANFG.spl~
  WRITE_BYTE 0x001c  4  //innate

  COPY ~%folder%/data/wands/B_WAN11.itm~ ~override~
        SAY NAME2 @900010021
        SAY DESC @900010022
//now, include in tier 1 wands:
    COPY ~%folder%/lib/wand_lists/wand_tier_list.tpa~ ~%folder%/lib/wand_lists/wand_tier_list.tpa~
      REPLACE_TEXTUALLY ~//B_WAN11~ ~B_WAN11~

 END //end SR Obscuring Mist
//dummy wand if not available

 ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_OBSCURING_MIST~)) BEGIN

  COPY_EXISTING ~WAND03.ITM~ ~override/B_WAN11.ITM~

 END
END

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Freshness         //////////////////
////////////////////////////////////////////////////////////////////////
//| of Freshness (N) | I |  as ZoSA | p235

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN10.ITM~ BEGIN
  COPY ~%folder%/data/wands/B_WAN10.itm~ ~override~
        SAY NAME2 @900010019
        SAY DESC @900010020
END

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of Teeth           //////////////////
////////////////////////////////////////////////////////////////////////
//| of Teeth (N)  | I | 2d6+2 missile dam (save 1/2) |
ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN01.ITM~ BEGIN
  COPY ~%folder%/data/wands/B_WAN01.itm~ ~override~
        SAY NAME2 @900010001
        SAY DESC @900010002
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////         Tier II Wands              ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
//////////////////            Gremlin Wand            //////////////////
////////////////////////////////////////////////////////////////////////

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN17.ITM~ BEGIN

//| Gremlin Wand (N) | II | Summon Gremlins |
//copy over the monsters:
COPY ~%folder%/data/wands/b_grem01.cre~ ~override~
          SAY NAME1 @900010049
          SAY NAME2 @900010049
//cre item
COPY ~%folder%/data/wands/b_grem01.itm~ ~override~


//copy over the table:
COPY ~%folder%/data/wands/B_GREM.2da~ ~override~

//add to sumtable
LAF cd_new_summon_table STR_VAR descript = "GREMLINS" 2da_file = B_GREM RET table END

//add the wand and ref sumtable

COPY ~%folder%/data/wands/B_WAN17.itm~ ~override~
          SAY NAME2 @900010033
          SAY DESC @900010034
  LPF ALTER_EFFECT INT_VAR match_opcode = 331 parameter2 = table END

END  //end gremlin wand

////////////////////////////////////////////////////////////////////////
//////////////////           Wand of Force            //////////////////
////////////////////////////////////////////////////////////////////////

//| of Force (N) | II |  2d6 crush damage and Push away cre (save 1/2 dam and no push) |
ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN08.ITM~ BEGIN

  COPY ~%folder%/data/wands/B_WAN08.itm~ ~override~
          SAY NAME2 @900010015
          SAY DESC @900010016
END

////////////////////////////////////////////////////////////////////////
//////////////////           Wand of Illusion         //////////////////
////////////////////////////////////////////////////////////////////////
//| of Illusion (N) | II |  Glammer; Ph. Force |


////////////////////////////////////////////////////////////////////////
//////////////////           Wand of Knock            //////////////////
////////////////////////////////////////////////////////////////////////
//| of Knock(N) | II | As Knock |   p237
ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN02.ITM~ BEGIN
  COPY ~%folder%/data/wands/B_WAN02.itm~ ~override~
        SAY NAME2 @900010003
        SAY DESC @900010004
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////         Tier III Wands             ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Lightning          //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~IWAND07.BAM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/IWAND07.BAM~ ~override~

END


ACTION_IF NOT FILE_EXISTS_IN_GAME ~WAND07.ITM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/WAND07.ITM~ ~override~
        SAY NAME1 @900010055
        SAY NAME2 @900010056
	SAY DESC @900010057
        SAY UNIDENTIFIED_DESC @900010058

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~MISC7N.ITM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/WAND07.ITM~ ~override/MISC7N.ITM~
        SAY NAME1 @900010055
        SAY NAME2 @900010056
	SAY DESC @900010057
        SAY UNIDENTIFIED_DESC @900010058

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~WANDREA.ITM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/WAND07.ITM~ ~override/WANDREA.ITM~
        SAY NAME1 @900010055
        SAY NAME2 @900010056
	SAY DESC @900010057
        SAY UNIDENTIFIED_DESC @900010058

END

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Fireballs          //////////////////
////////////////////////////////////////////////////////////////////////

//| of Fireballs (N) | III | As Fireball Spell | 6d6 dam

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN05.ITM~ BEGIN

  COPY ~%folder%/data/wands/B_WAN05.itm~ ~override~
        SAY NAME2 @900010009
        SAY DESC @900010010
END

////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////          Tier IV Wands             ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
//////////////////       Wand of Freezing Death       //////////////////
////////////////////////////////////////////////////////////////////////

ACTION_IF NOT FILE_EXISTS_IN_GAME ~IWANDFRE.BAM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/IWANDFRE.BAM~ ~override~

END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~WANDFRE.ITM~ BEGIN
//tooltip
LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@3270000) // ice lance
  $tip(2) = RESOLVE_STR_REF (@2040002) // sss
  $tip(3) = RESOLVE_STR_REF (@900010063) // Ice Storm
STR_VAR
  item = WANDFRE // file name
END

  COPY ~%folder%/data/arcane_crafting/wand/WANDFRE.ITM~ ~override~
        SAY NAME1 @900010055
        SAY NAME2 @900010059
	SAY DESC @900010060
        SAY UNIDENTIFIED_DESC @900010058
           LPF ALTER_EFFECT INT_VAR  STR_VAR match_resource = ~SPWI327~ resource = EVAL ~WNDFRE1~  END      //
           LPF ALTER_EFFECT INT_VAR  STR_VAR match_resource = ~SPWI204~ resource = EVAL ~WNDFRE2~  END      //
           LPF ALTER_EFFECT INT_VAR  STR_VAR match_resource = ~SPWI404~ resource = EVAL ~WNDFRE3~  END      //

 ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_ICELANCE~)) BEGIN  //check for spells and add if missing

  //Projectile
     ADD_PROJECTILE ~%folder%/data/iwd_spells/wizard_3/IDPRO251.pro~   //projectile

  //bam
     COPY ~%folder%/data/iwd_spells/wizard_3/ICELANT.BAM~ ~override~   //animation

  //sounds
     COPY ~%folder%/data/iwd_spells/wizard_3/#TRA_19.wav~ ~override~   //animation

  //Spell itself
     COPY ~%folder%/data/iwd_spells/wizard_3/b_w327.spl~ ~override/WNDFRE1.SPL~  //
        SAY NAME1 @3270000
        SAY NAME2 @3270000
        SAY UNIDENTIFIED_DESC @3270003
	SAY DESC @3270003
         LPF cd_apply_batch STR_VAR array_name = cd_full_stun_arrays END
         LPF ALTER_EFFECT
           INT_VAR match_opcode = 324            //
           STR_VAR resource = EVAL ~WNDFRE1~
         END      //
        WRITE_SHORT 0x001c 2 //spell type: Cleric
      COPY_EXISTING ~WNDFRE1.spl~ ~override~
          SET strref = RESOLVE_STR_REF(@3270002)
          LPF ALTER_EFFECT
             INT_VAR match_opcode = 139 match_parameter1 = 14024 parameter1 = %strref%   END      //
      //add icons and projectile
      COPY_EXISTING ~WNDFRE1.spl~ ~override~
          LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO251% STR_VAR icon = EVAL  ~WNDFRE1B~ END
      COPY ~%folder%/data/iwd_spells/wizard_3/SPWI327a.bam~ ~override/WNDFRE1a.bam~
      COPY ~%folder%/data/iwd_spells/wizard_3/SPWI327b.bam~ ~override/WNDFRE1b.bam~
      COPY ~%folder%/data/iwd_spells/wizard_3/SPWI327c.bam~ ~override/WNDFRE1c.bam~
      //add bams to spl
      COPY_EXISTING ~WNDFRE1.spl~ ~override~
          WRITE_EVALUATED_ASCII 0x003a ~WNDFRE1C~

 END   //end icelance
 //icelance in game
  ACTION_IF (FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_ICELANCE~)) BEGIN  //check for spells and add if missing
//Ice Storm
  LAF RES_NUM_OF_SPELL_NAME
    STR_VAR spell_name = ~WIZARD_ICELANCE~
    RET spell_res
  END
  COPY_EXISTING ~%spell_res%.spl~ ~override/WNDFRE1.SPL~
        WRITE_SHORT 0x001c 2 //spell type: Cleric
  END

 ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SNILLOCS_SNOWBALL_SWARM~)) BEGIN  //check for spells and add if missing
   //Projectile
      ADD_PROJECTILE ~%folder%/data/iwd_spells/wizard_2/IDPRO217.pro~   //projectile
   //VVC
      COPY ~%folder%/data/iwd_spells/wizard_2/#SSSWARM.vvc~ ~override~   //animation
   //bam
      COPY ~%folder%/data/iwd_spells/wizard_2/SSSWART.BAM~ ~override~   //animation
      COPY ~%folder%/data/iwd_spells/wizard_2/SSSWARR.BAM~ ~override~   //animation
      COPY ~%folder%/data/iwd_spells/wizard_2/SSSWARX.BAM~ ~override~   //animation
   //sounds
      COPY ~%folder%/data/iwd_spells/wizard_2/TRA_18.WAV~ ~override~   //
      COPY ~%folder%/data/iwd_spells/wizard_2/RNG_M02.WAV~ ~override~   //
   //Spell itself
      COPY ~%folder%/data/iwd_spells/wizard_2/b_w204.spl~ ~override/WNDFRE2.SPL~
        SAY NAME1 @2040002
        SAY NAME2 @2040002
        SAY UNIDENTIFIED_DESC @2040003
	SAY DESC @2040003
          LPF ALTER_EFFECT INT_VAR match_opcode = 318  STR_VAR resource = EVAL ~WNDFRE2~ END      //
          LPF ALTER_EFFECT  INT_VAR match_opcode = 324  STR_VAR resource = EVAL ~WNDFRE2~  END      //
          WRITE_SHORT 0x001c 2 //spell type: Cleric
   //add icons and projectile
        LPF ALTER_SPELL_HEADER INT_VAR projectile = %IDPRO217% STR_VAR icon = EVAL  ~WNDFRE2B~ END
          COPY ~%folder%/data/iwd_spells/wizard_2/SPWI204a.bam~ ~override/WNDFRE2a.bam~
          COPY ~%folder%/data/iwd_spells/wizard_2/SPWI204b.bam~ ~override/WNDFRE2b.bam~
          COPY ~%folder%/data/iwd_spells/wizard_2/SPWI204c.bam~ ~override/WNDFRE2c.bam~
   //add bams to spl
        COPY_EXISTING ~WNDFRE2.spl~ ~override~
           WRITE_EVALUATED_ASCII 0x003a ~WNDFRE2C~

 END  //end SSS
 //sss in game
  ACTION_IF (FILE_CONTAINS_EVALUATED(~spell.ids~ ~WIZARD_SNILLOCS_SNOWBALL_SWARM~)) BEGIN  //check for spells and add if missing
  LAF RES_NUM_OF_SPELL_NAME
    STR_VAR spell_name = ~WIZARD_SNILLOCS_SNOWBALL_SWARM~
    RET spell_res
  END
COPY_EXISTING ~%spell_res%.spl~ ~override/WNDFRE2.SPL~
        WRITE_SHORT 0x001c 2 //spell type: Cleric
  END

//Ice Storm
  LAF RES_NUM_OF_SPELL_NAME
    STR_VAR spell_name = ~WIZARD_ICE_STORM~
    RET spell_res
  END
COPY_EXISTING ~%spell_res%.spl~ ~override/WNDFRE3.SPL~
        WRITE_SHORT 0x001c 2 //spell type: Cleric
END   //end wand of fr death

////////////////////////////////////////////////////////////////////////
//////////////////           Wand of Armory           //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~IWANDARM.BAM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/IWANDARM.BAM~ ~override~

END


ACTION_IF NOT FILE_EXISTS_IN_GAME ~WANDARM.ITM~ BEGIN
//tooltip
LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@900010064) // shield
  $tip(2) = RESOLVE_STR_REF (@900010065) // ghost armor
STR_VAR
  item = WANDARM // file name
END

  COPY ~%folder%/data/arcane_crafting/wand/WANDARM.ITM~ ~override~
        SAY NAME1 @900010055
        SAY NAME2 @900010061
	SAY DESC @900010062
        SAY UNIDENTIFIED_DESC @900010058
           LPF ALTER_EFFECT INT_VAR  STR_VAR match_resource = ~SPWI114~ resource = EVAL ~WNDARM1~  END      //
           LPF ALTER_EFFECT INT_VAR  STR_VAR match_resource = ~SPWI317~ resource = EVAL ~WNDARM2~  END      //

//shield
  LAF RES_NUM_OF_SPELL_NAME
    STR_VAR spell_name = ~WIZARD_SHIELD~
    RET spell_res
  END
COPY_EXISTING ~%spell_res%.spl~ ~override/WNDARM1.SPL~
        WRITE_SHORT 0x001c 2 //spell type: Cleric

//ghost armor
  LAF RES_NUM_OF_SPELL_NAME
    STR_VAR spell_name = ~WIZARD_GHOST_ARMOR~
    RET spell_res
  END
COPY_EXISTING ~%spell_res%.spl~ ~override/WNDARM2.SPL~
        WRITE_SHORT 0x001c 2 //spell type: Cleric

END  //end wand of armory

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Polymorph          //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~IWAND09.BAM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/IWAND09.BAM~ ~override~

END


ACTION_IF NOT FILE_EXISTS_IN_GAME ~WAND09.ITM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/WAND09.ITM~ ~override~
        SAY NAME1 @900010055
        SAY NAME2 @900010066
	SAY DESC @900010067
        SAY UNIDENTIFIED_DESC @900010058

END

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Spiders            //////////////////
////////////////////////////////////////////////////////////////////////
//| of Spiders (N) | ? |  Spider Spawn; Web |

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN14.ITM~ BEGIN
  COPY ~%folder%/data/wands/B_WAN14.itm~ ~override~
        SAY NAME2 @900010027
        SAY DESC @900010028

 //tooltip
 LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@900010047) // Summon Giant Spider
  $tip(2) = RESOLVE_STR_REF (@900010048) // Web
 STR_VAR
  item = B_WAN14 // file name
 END
END

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Healing            //////////////////
////////////////////////////////////////////////////////////////////////

//| of Healing (N)  | ? |  Cure 2d6+2 HP at range |

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN13.ITM~ BEGIN
  COPY ~%folder%/data/wands/B_WAN13.itm~ ~override~
        SAY NAME2 @900010023
        SAY DESC @900010024
END
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////          Tier V Wands              ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Cloudkill          //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~IWAND13.BAM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/IWAND13.BAM~ ~override~

END


ACTION_IF NOT FILE_EXISTS_IN_GAME ~WAND13.ITM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/WAND13.ITM~ ~override~
        SAY NAME1 @900010055
        SAY NAME2 @900010068
	SAY DESC @900010069
        SAY UNIDENTIFIED_DESC @900010058

END


////////////////////////////////////////////////////////////////////////
//////////////////            Bonewand                //////////////////
////////////////////////////////////////////////////////////////////////
//| Bonewand  (N) | ? | Shards of Bone (6d4 piercing dam save for 1/2); Animate Dead as Spell |
ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN07.ITM~ BEGIN
//Projectile
  ADD_PROJECTILE ~%folder%/data/iwd_spells/priest_4/idpro303.pro~   //projectile
//probam
  COPY ~%folder%/data/iwd_spells/priest_4/TSPRAYT.bam~ ~override~   //
  COPY ~%folder%/data/wands/B_WAN07.itm~ ~override~
          SAY NAME2 @900010013
          SAY DESC @900010014
  LPF ALTER_ITEM_HEADER
    INT_VAR  header = 1 projectile = %idpro303%
  END
//tooltip
 LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@900010039) // Boneshard
  $tip(2) = RESOLVE_STR_REF (@900010040) // Animate Skeleton Warrior
 STR_VAR
  item = B_WAN07 // file name
 END
END

////////////////////////////////////////////////////////////////////////
//////////////////            Wand of Eyes            //////////////////
////////////////////////////////////////////////////////////////////////
//| of Eyes (N) | V |   Wizard Eye; See Invisible |  b_wan09
//add protection from B_WAND09 to WIzEye spell

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN07.ITM~ BEGIN
//WIzEye
//copy wiz eye, add immunity to secspell:
  COPY_EXISTING ~SPWI425.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR multi_match = 1 match_opcode = 206 STR_VAR resource = ~B_WNDEY~ END  //Protection from secspell
//create secspell
  COPY_EXISTING ~SPWI425.spl~ ~override/B_WNDEY.spl~
    WRITE_BYTE 0x001c  4  //innate no wiz spell!

//CureBlind

//Detect Invisibility
//create secspell
  COPY_EXISTING ~SPWI203.spl~ ~override/B_WNDDI.spl~
    WRITE_BYTE 0x001c  4  //innate no wiz spell!

//actual wand

  COPY ~%folder%/data/wands/B_WAN09.itm~ ~override~
        SAY NAME2 @900010017
        SAY DESC @900010018

//tooltip
 LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@900010044) // Wizard Eye
  $tip(2) = RESOLVE_STR_REF (@900010045) // Remove Blindness
  $tip(3) = RESOLVE_STR_REF (@900010046) // Detect Invisibility
 STR_VAR
  item = B_WAN09 // file name
 END
END

////////////////////////////////////////////////////////////////////////
//////////////////         Wand of Illumination       //////////////////
////////////////////////////////////////////////////////////////////////
//| of Illumination (N) | V | As Sunfire |
//just use the fireball pro...
ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN19.itm~ BEGIN

  COPY ~%folder%/data/wands/B_WAN19.itm~ ~override~
        SAY NAME2 @900010037
        SAY DESC @900010038

END

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Swarming          //////////////////
////////////////////////////////////////////////////////////////////////
//| of Swarming (N)  | ? |  as Summon Insects |
ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN15.itm~ BEGIN

  COPY ~%folder%/data/wands/B_WAN15.itm~ ~override~
        SAY NAME2 @900010029
        SAY DESC @900010030

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////          Tier VI Wands             ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
//////////////////      Wand of Spell Striking        //////////////////
////////////////////////////////////////////////////////////////////////

ACTION_IF NOT FILE_EXISTS_IN_GAME ~WAND18.itm~ BEGIN
  COPY ~%folder%/data/arcane_crafting/wand/IWAND18.bam~ ~override~
  COPY ~%folder%/data/arcane_crafting/wand/WAND18.ITM~ ~override~
        SAY NAME1 @68077
        SAY NAME2 @68083
        SAY UNIDENTIFIED_DESC @68080
	SAY DESC @68084
  LPF ALTER_EFFECT INT_VAR STR_VAR match_resource = SPWI513 resource = B_WA101 END      //   breach
  LPF ALTER_EFFECT INT_VAR STR_VAR match_resource = SPWI608 resource = B_WA102 END      // sp piercing

//tooltip
LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@68085) // Breach
  $tip(2) = RESOLVE_STR_REF (@68086) // Pierce Magic
STR_VAR
  item = WAND18 // file name
END
//spells
LAF RES_NUM_OF_SPELL_NAME
  STR_VAR spell_name = ~WIZARD_BREACH~
  RET spell_res
END
  COPY_EXISTING ~%spell_res%.spl~ ~override/B_WA101.spl~
    LPF ALTER_SPELL_TYPE INT_VAR   spell_type = 4 END

  COPY ~%folder%/data/arcane_crafting/wand/B_WA102.SPL~ ~override~
        SAY NAME1 @68086
        SAY NAME2 @68086
        LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (@68087) STR_VAR END  //Spell Protections Removed

  COPY ~%folder%/data/arcane_crafting/wand/SPATTCK1.BAM~ ~override~

END  //end add wand

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Cursing           //////////////////
////////////////////////////////////////////////////////////////////////

//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~WAND19.itm~ BEGIN
  COPY ~%folder%/data/arcane_crafting/wand/IWAND19.bam~ ~override~

  COPY ~%folder%/data/arcane_crafting/wand/WAND19.ITM~ ~override~
        SAY NAME1 @68077
        SAY NAME2 @68078
        SAY UNIDENTIFIED_DESC @68080
	SAY DESC @68079
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (@68081) match_parameter1 = 37800 STR_VAR END  //blind notification
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (@68082) match_parameter1 = 35952 STR_VAR END  //deaf notification
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = RESOLVE_STR_REF (@68083) match_parameter1 = 37805 STR_VAR END  //Silence notification
END  //end add wand

////////////////////////////////////////////////////////////////////////
//////////////////            Animated Wand           //////////////////
////////////////////////////////////////////////////////////////////////
//| Animated Wand (N) | VI | Transforms into a blade that fights for the caster |

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN06.itm~ BEGIN

 COPY ~%folder%/data/wands/B_WAN06.itm~ ~override~
          SAY NAME2 @900010011
          SAY DESC @900010012
//cre
 COPY ~%folder%/data/wands/B_WAN06.cre~ ~override~
          SAY NAME1 @900010011
          SAY NAME2 @900010011
//crea weapon  IPSION.itm
 COPY ~%folder%/data/wands/B_WAN06A.itm~ ~override~
          SAY NAME1 @900010011
          SAY NAME2 @900010011
//just in case
 COPY ~%folder%/data/wands/IPSION.itm~ ~override~
END

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Banishment        //////////////////
////////////////////////////////////////////////////////////////////////
//| of Banishment (Ns) | VI | Removes summoned creatures, evil outsiders save or destroyed/banished |
//only installed if SR is installed 

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN12.itm~ BEGIN

 ACTION_IF (FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_BANISHMENT~)) BEGIN  //SR Banishment

 LAF RES_NUM_OF_SPELL_NAME
  STR_VAR spell_name = ~CLERIC_BANISHMENT~
  RET spell_res
 END

 COPY_EXISTING ~%spell_res%.spl~ ~override/B_WANBA.spl~
  WRITE_BYTE 0x001c  4  //innate
  LPF ALTER_EFFECT
      INT_VAR match_opcode = 177 savingthrow = 8           //  save wand
  END      //
  LPF CLONE_EFFECT
      INT_VAR match_opcode = 177 savingthrow = 0  timing = 0 duration = 18
      STR_VAR resource = ~B_WANST~   //Stun
  END

  //stun eff
  COPY ~%folder%/data/wands/B_WANST.eff~ ~override~   //stun effect

  //actual wand
  COPY ~%folder%/data/wands/B_WAN12.itm~ ~override~
        SAY NAME2 @900010023
        SAY DESC @900010024


 END //end check for banishment
//dummy wand if not available
 ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_BANISHMENT~)) BEGIN

  COPY_EXISTING ~WAND03.ITM~ ~override/B_WAN12.ITM~

 END

END//end check for wand




////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////          Tier VII Wands            ////////////////////////////////////
////////////////////////////////////                                    ////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Command           //////////////////
////////////////////////////////////////////////////////////////////////
//| of Command (N)  | VII |  as Dominate |

  COPY ~%folder%/data/wands/B_WAN04.itm~ ~override~
        SAY NAME2 @900010007
        SAY DESC @900010008

//text notification (BG2EE/EET is the default)
  ACTION_IF GAME_IS ~bgee~ BEGIN

  COPY_EXISTING ~B_WAN04.itm~ ~override~
   LPF ALTER_EFFECT
      INT_VAR match_opcode = 139            //
      parameter1 = 26206
  END      //

  END
  ACTION_IF GAME_IS ~iwdee~ BEGIN

  COPY_EXISTING ~B_WAN04.itm~ ~override~
   LPF ALTER_EFFECT
      INT_VAR match_opcode = 139            //
      parameter1 = 35544
   END      //

  END

////////////////////////////////////////////////////////////////////////
//////////////////          Wand of Corosion          //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~IWANDCOR.BAM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/IWANDCOR.BAM~ ~override~

END


ACTION_IF NOT FILE_EXISTS_IN_GAME ~WANDCOR.ITM~ BEGIN

  COPY ~%folder%/data/arcane_crafting/wand/WANDCOR.ITM~ ~override~
        SAY NAME1 @900010055
        SAY NAME2 @900010070
	SAY DESC @900010071
        SAY UNIDENTIFIED_DESC @900010058

END

////////////////////////////////////////////////////////////////////////
//////////////////        Wand of the Heavens         //////////////////
////////////////////////////////////////////////////////////////////////
ACTION_IF NOT FILE_EXISTS_IN_GAME ~TTWAND.ITM~ BEGIN

  COPY_EXISTING ~WAND11.ITM~ ~override/TTWAND.ITM~

END

////////////////////////////////////////////////////////////////////////
//////////////////           Wand of Death            //////////////////
////////////////////////////////////////////////////////////////////////
//| of Death (N) | VII | Death Spell? |

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN16.ITM~ BEGIN

  COPY ~%folder%/data/wands/B_WAN16.itm~ ~override~
        SAY NAME2 @900010031
        SAY DESC @900010032

END

////////////////////////////////////////////////////////////////////////
//////////////////           Wand of Darkness         //////////////////
////////////////////////////////////////////////////////////////////////

ACTION_IF NOT FILE_EXISTS_IN_GAME ~B_WAN18.ITM~ BEGIN

  COPY ~%folder%/data/wands/B_WAN18.itm~ ~override~
        SAY NAME2 @900010035
        SAY DESC @900010036

//tooltip
 LAF tooltips INT_VAR
  $tip(1) = RESOLVE_STR_REF (@900010041) // Blindness
  $tip(2) = RESOLVE_STR_REF (@900010042) // Smite Good
  $tip(3) = RESOLVE_STR_REF (@900010043) // Summon Shadow
 STR_VAR
  item = B_WAN18 // file name
 END

END

